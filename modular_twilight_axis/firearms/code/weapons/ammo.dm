/obj/projectile/bullet
	var/silver = FALSE
	var/critfactor = 1

/**
 * Special runelock ammo
 * Meant to be LIMITED, but reusable
 */

/obj/projectile/bullet/reusable/twilight_runelock
	name = "runed sphere"
	desc = "Небольшой, идеально круглый металлический шар, покрытый псайдонитскими рунами. Смертоносен на высокой скорости."
	damage = 90
	armor_penetration = 75
	speed = 0.6
	damage_type = BRUTE
	icon = 'modular_twilight_axis/firearms/icons/ammo.dmi'
	icon_state = "musketball_runed"
	ammo_type = /obj/item/ammo_casing/caseless/twilight_lead/runelock
	range = 20
	hitsound = 'sound/combat/hits/hi_bolt (2).ogg'
	embedchance = 100
	woundclass = BCLASS_STAB
	flag = "piercing"

/obj/projectile/bullet/reusable/twilight_runelock/blessed
	name = "blessed sphere"
	desc = "Небольшой, идеально круглый шар, изготовленный из чистого серебра. Такие боеприпасы создаются лучшими из отавианских кузнецов и освящяются лично Великим Магистром. Смертоностны против нежити, но весьма эффективны и против других еретиков."
	damage = 100
	ammo_type = /obj/item/ammo_casing/caseless/twilight_lead/runelock/blessed
	icon_state = "musketball_blessed"
	silver = TRUE

/**
 * Generic ammo used by handgonnes and arquebuses
 */
/obj/projectile/bullet/twilight_lead
	name = "lead sphere"
	desc = "Небольшая свинцовая сфера. Хорошо сочетается с порохом."
	damage = 100	//higher damage than crossbow
	damage_type = BRUTE
	icon = 'modular_twilight_axis/firearms/icons/ammo.dmi'
	icon_state = "musketball_proj"
	ammo_type = /obj/item/ammo_casing/caseless/twilight_lead
	range = 25		
	hitsound = 'sound/combat/hits/hi_arrow2.ogg'
	embedchance = 100
	woundclass = BCLASS_STAB
	flag = "piercing"
	armor_penetration = 75
	speed = 0.1

/obj/projectile/bullet/twilight_lead/silver
	name = "silver sphere"
	desc = "Небольшая серебряная сфера. Мягче, чем свинцовая пуля, но крайне эффективна против нежити."
	ammo_type = /obj/item/ammo_casing/caseless/twilight_lead/silver
	damage = 75
	armor_penetration = 60
	silver = TRUE
	critfactor = 0.8

/obj/projectile/bullet/twilight_cannonball
	name = "cannonball"
	desc = "Крупная свинцовая сфера. Важен не размер ствола, а размер отверстия, что он делает в вашем противнике."
	damage = 60
	damage_type = BRUTE
	icon = 'modular_twilight_axis/firearms/icons/ammo.dmi'
	icon_state = "musketball_proj"
	ammo_type = /obj/item/ammo_casing/caseless/twilight_cannonball
	range = 25		
	hitsound = 'sound/combat/hits/hi_arrow2.ogg'
	embedchance = 0
	woundclass = BCLASS_STAB
	flag = "piercing"
	armor_penetration = 105
	speed = 0.1

/obj/projectile/bullet/twilight_grapeshot
	name = "grapeshot"
	desc = "Плотно упакованный в бумагу набор небольших металлических шариков. Хорошо сочетается с порохом."
	damage = 20
	damage_type = BRUTE
	icon = 'modular_twilight_axis/firearms/icons/ammo.dmi'
	icon_state = "musketball_proj"
	ammo_type = /obj/item/ammo_casing/caseless/twilight_cannonball/grapeshot
	range = 15
	hitsound = 'sound/combat/hits/hi_arrow2.ogg'
	embedchance = 100
	woundclass = BCLASS_STAB
	flag = "piercing"
	armor_penetration = 75
	speed = 0.1
	critfactor = 0.67

/obj/projectile/bullet/on_hit(atom/target, blocked = FALSE)
	. = ..()
	if(isliving(firer) && (istype(fired_from, /obj/item/gun/ballistic/twilight_firearm) || istype(fired_from, /obj/item/gun/ballistic/revolver/grenadelauncher/twilight_runelock)))
		var/mob/living/M = firer
//		var/obj/item/gun/G = fired_from
		var/skill = (M?.mind ? M.get_skill_level(/datum/skill/combat/twilight_firearms) : 1)
		if(isliving(target))
			var/mob/living/T = target
			if(skill >= 1) //Exp gain from firing a gun
				if(isanimal(T) && (T.stat != DEAD || (T.stat == DEAD && T.timeofdeath == world.time)))
					adjust_experience(M, /datum/skill/combat/twilight_firearms, M.STAINT * 3)
				else if(ishuman(T) && (T.stat != DEAD || (T.stat == DEAD && T.timeofdeath == world.time)))
					adjust_experience(M, /datum/skill/combat/twilight_firearms, M.STAINT * 6)
					var/list/screams = list("painscream", "paincrit") //Simulating paincrit on hit
					var/check = rand(1, 20)
					if(check > T.STACON)
						T.emote(screams)
						T.Knockdown(rand(15,30))
						T.Immobilize(rand(30,60))
			if(silver) //Silver bullet effects
				if(T.mind)
					var/datum/antagonist/werewolf/W = T.mind.has_antag_datum(/datum/antagonist/werewolf/)
					var/datum/antagonist/vampire/V = T.mind.has_antag_datum(/datum/antagonist/vampire)
					var/datum/antagonist/vampire/V_lord = T.mind.has_antag_datum(/datum/antagonist/vampire/)
					if(V)
						T.visible_message("<font color='white'>The silver weapon weakens the curse temporarily!</font>")
						to_chat(T, span_userdanger("I'm hit by my BANE!"))
						//T.apply_status_effect(/datum/status_effect/debuff/silver_curse)
					if(V_lord)
						if(V_lord.research_points < 4 && !V)
							T.visible_message("<font color='white'>The silver weapon weakens the curse temporarily!</font>")
							to_chat(T, span_userdanger("I'm hit by my BANE!"))
							//T.apply_status_effect(/datum/status_effect/debuff/silver_curse)
						if(V_lord.research_points == 4 && !V)
							to_chat(T, "<font color='red'> The silver weapon fails!</font>")
							T.visible_message(T, span_userdanger("This feeble metal can't hurt me, I AM ANCIENT!"))
					if(W && W.transformed == TRUE)
						T.visible_message("<font color='white'>The silver weapon weakens the curse temporarily!</font>")
						to_chat(T, span_userdanger("I'm hit by my BANE!"))
						//T.apply_status_effect(/datum/status_effect/debuff/silver_curse)
			if(blocked == 0) //Handle crits. Gunpowder weapons have a separate crit roll that ignores bodypart health
				if(iscarbon(T))
					var/zone = def_zone
					var/obj/item/bodypart/affecting = T.get_bodypart(zone)
					if(affecting)
						var/check_crit_against_con = rand(10, 20)
						check_crit_against_con *= critfactor * (M.STAPER > 10 ? M.STAPER / 10 : 1)
						if(check_crit_against_con > (T.STACON))
							if(T.getarmor(zone, flag) >= (armor_penetration / 2))
								if(prob(60))
									affecting.twilight_gunpowder_crit(woundclass, zone_precise = zone, crit_message = TRUE)
							else
								if(prob(90))
									affecting.twilight_gunpowder_crit(woundclass, zone_precise = zone, crit_message = TRUE)

/obj/projectile/bullet/twilight_cannonball/on_hit(atom/target, blocked = FALSE)
	. = ..()
	var/turf/T = get_turf(target)
	explosion(T, devastation_range = 0, heavy_impact_range = 0, light_impact_range = 1, smoke = TRUE, soundin = pick('sound/misc/explode/bottlebomb (1).ogg','sound/misc/explode/bottlebomb (2).ogg'))

/obj/item/ammo_casing/caseless/twilight_lead
	name = "lead sphere"
	desc = "Небольшая свинцовая сфера. Хорошо сочетается с порохом."
	projectile_type = /obj/projectile/bullet/twilight_lead
	caliber = "lead_sphere"
	icon = 'modular_twilight_axis/firearms/icons/ammo.dmi'
	icon_state = "musketball"
	dropshrink = 0.5
	max_integrity = 0.1

/obj/item/ammo_casing/caseless/twilight_lead/runelock
	name = "runed sphere"
	desc = "Небольшой, идеально круглый металлический шар, покрытый псайдонитскими рунами. Смертоносен на высокой скорости."
	projectile_type = /obj/projectile/bullet/reusable/twilight_runelock
	caliber = "runed_sphere"
	icon = 'modular_twilight_axis/firearms/icons/ammo.dmi'
	icon_state = "musketball_runed"
	possible_item_intents = list(/datum/intent/use)
	max_integrity = 0
	w_class = WEIGHT_CLASS_TINY
	smeltresult = /obj/item/rogueore/iron

/obj/item/ammo_casing/caseless/twilight_lead/runelock/Initialize()
	. = ..()
	var/filter = src.get_filter("rune_filter")
	if(!filter)
		src.add_filter("rune_filter", 2, list("type" = "outline", "color" = rgb(112, 28, 28, 1), "alpha" = 200, "size" = 2))

/obj/item/ammo_casing/caseless/twilight_lead/runelock/equipped(mob/living/user)
	if(!HAS_TRAIT(user, TRAIT_INQUISITION) && !(user.STAINT >= 15) && !(user.patron?.type == /datum/patron/old_god))
		to_chat(user, "<font color='yellow'>The [name] is extremely hot to touch! It burns your hand!</font>")
		var/def_zone = "[(user.active_hand_index == 2) ? "r" : "l" ]_hand"
		user.apply_damage(rand(5,15), BURN, def_zone)
		src.forceMove(get_turf(user))
	..()

/obj/item/ammo_casing/caseless/twilight_lead/silver
	name = "silver sphere"
	desc = "Небольшая серебряная сфера. Мягче, чем свинцовая пуля, но крайне эффективна против нежити."
	projectile_type = /obj/projectile/bullet/twilight_lead/silver
	icon_state = "musketball_silver"

/obj/item/ammo_casing/caseless/twilight_lead/runelock/blessed
	name = "blessed sphere"
	desc = "Небольшой, идеально круглый шар, изготовленный из чистого серебра. Такие боеприпасы создаются лучшими из отавианских кузнецов и освящяются лично Великим Магистром. Смертоностны против нежити, но весьма эффективны и против других еретиков."
	projectile_type = /obj/projectile/bullet/reusable/twilight_runelock/blessed
	icon_state = "musketball_blessed"
	w_class = WEIGHT_CLASS_TINY
	smeltresult = /obj/item/rogueore/silver

/obj/item/ammo_casing/caseless/twilight_cannonball
	name = "lead cannonball"
	desc = "Крупная свинцовая сфера. Важен не размер ствола, а размер отверстия, что он делает в вашем противнике."
	projectile_type = /obj/projectile/bullet/twilight_cannonball
	caliber = "cannonball"
	icon = 'modular_twilight_axis/firearms/icons/ammo.dmi'
	icon_state = "cball"
	dropshrink = 0.5
	max_integrity = 0.1
	grid_width = 32
	grid_height = 64

/obj/item/ammo_casing/caseless/twilight_cannonball/grapeshot
	name = "grapeshot"
	desc = "Плотно упакованный в бумагу набор небольших металлических шариков. Хорошо сочетается с порохом."
	projectile_type = /obj/projectile/bullet/twilight_grapeshot
	caliber = "cannonball"
	icon = 'modular_twilight_axis/firearms/icons/ammo.dmi'
	icon_state = "grapeshot"
	dropshrink = 0.5
	max_integrity = 0.1
	pellets = 6
	variance = 30
